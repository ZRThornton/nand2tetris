// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    //// Get x input
    Mux16(a=x,b=false,sel=zx,out=x1);
    Not16(in=x1,out=xn);
    Mux16(a=x1,b=xn,sel=nx,out=xin);

    //// Get y input
    Mux16(a=y,b=false,sel=zy,out=y1);
    Not16(in=y1,out=yn);
    Mux16(a=y1,b=yn,sel=ny,out=yin);

    //// Computations
    Add16(a=xin, b=yin, out=xplusy);
    And16(a=xin, b=yin, out=xandy);
    Mux16(a=xandy, b=xplusy, sel=f, out=o);

    //// Not the output? in is o
    Not16(in=o,out=nout);
    Mux16(a=o,b=nout,sel=no,out=out,out[0..7]=oz1, out[8..14]=oz2,out[15]=oz3);

    ////Check zr
    Or8Way(in=oz1,out=zor1);
    Or8Way(in[0..6]=oz2,in[7]=oz3,out=zor2);
    Or(a=zor1,b=zor2,out=zor);
    Not(in=zor, out=zr);

    //// Check ng
    And(a=oz3,b=oz3,out=ng);
}